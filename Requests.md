# Запросы

## Соединения таблиц

### Исходная таблица

|    Люди   |         Характеристики        |
|:---------:|:-----------------------------:|
|Алена      |   Алена           21          |
|Алефтина   |   Алефтина        18          |
|Владимир   |   Павел           25          |
|Владислав  |   Петр            30          |
|           |   Порфирий        50          |


### Внутреннее соединение 

> Используются, чтобы сопоставить (соединить) строки одной таблицы строкам другой таблицы по определенному признаку

#### Результат внутреннего соединения по имени:

|ИмяИзТаблицы1 |  ИмяИзТаблицы2 |  ВозрастИзТаблицы2|
|:------------:|:--------------:|:-----------------:|
|Алена         |  Алена         |  21               |
|Алефтина      |  Алефтина      |  18               |


### Левое соединение 

> Внутреннее соединение + строки из левой таблицы, для которых не найдены соответствия

#### Результат левого внешнего соединения по имени:

|ИмяИзТаблицы1 |   ИмяИзТаблицы2 |  ВозрастИзТаблицы2 |
|:------------:|:---------------:|:------------------:|
|Алена         |  Алена          | 21                 |
|Алефтина      |  Алефтина       | 18                 |
|Владимир      |  NULL           | NULL               |
|Владислав     |  NULL           | NULL               |


### Правое соединение 

> Внутреннее соединение + строки из правой таблицы, для которых не найдены соответствия

#### Результат правого внешнего соединения по имени:

|ИмяИзТаблицы1 |   ИмяИзТаблицы2 |  ВозрастИзТаблицы2 |
|:------------:|:---------------:|:------------------:|
|Алена         |  Алена          | 21                 |
|Алефтина      |  Алефтина       | 18                 |
|NULL          |  Павел          | 25                 |
|NULL          |  Петр           | 30                 |
|NULL          |  Порфирий       | 50                 |


### Полное соединение 

> Левое соединение + правое соединение.

#### Результат полного внешнего соединения по имени:

|ИмяИзТаблицы1 |   ИмяИзТаблицы2 |  ВозрастИзТаблицы2 |
|:------------:|:---------------:|:------------------:|
|Алена         | Алена           | 21                 |
|Алефтина      | Алефтина        | 18                 |
|Владимир      | NULL            | NULL               |
|Владислав     | NULL            | NULL               |
|NULL          | Павел           | 25                 |
|NULL          | Петр            | 30                 |
|NULL          | Порфирий        | 50                 |


## Вложенные таблицы

### 1 вариант
```bsl
ВЫБРАТЬ
  ОказаниеУслуг.Номер КАК НомерДокумента,
  ОказаниеУслуг.Дата КАК ДатаДокумента,
  ОказаниеУслуг.Контрагент КАК Клиент,
  ОказаниеУслуг.Контрагент.Телефон КАК Телефон,
  ОказаниеУслуг.Услуги.(
    Ссылка КАК Ссылка,
    НомерСтроки КАК НомерСтроки,
    Услуга КАК Услуга,
    Сумма КАК Сумма,
    Сотрудник КАК Сотрудник,
    ПроцентСотрудника КАК ПроцентСотрудника,
    СуммаСотрудника КАК СуммаСотрудника
  ) КАК Услуги
ИЗ
  Документ.ОказаниеУслуг КАК ОказаниеУслуг
```

### 2 вариант(сокращенный)
```bsl
ВЫБРАТЬ
  ОказаниеУслуг.Номер КАК НомерДокумента,
  ОказаниеУслуг.Дата КАК ДатаДокумента,
  ОказаниеУслуг.Контрагент КАК Клиент,
  ОказаниеУслуг.Контрагент.Телефон КАК Телефон,
  ОказаниеУслуг.Услуги.* 
ИЗ
  Документ.ОказаниеУслуг КАК ОказаниеУслуг
```

### Псевдонимы вложенной таблицы
```bsl
ВЫБРАТЬ
  ОказаниеУслуг.Номер КАК НомерДокумента,
  ОказаниеУслуг.Дата КАК ДатаДокумента,
  ОказаниеУслуг.Контрагент КАК Клиент,
  ОказаниеУслуг.Контрагент.Телефон КАК Телефон,
  ОказаниеУслуг.Услуги.(
    Услуга КАК Услуга,
    Сумма КАК СуммаЗаплаченная
  ) КАК УслугиОказанные
ИЗ
  Документ.ОказаниеУслуг КАК ОказаниеУслуг
```


## Условия запроса

### Проверка на группу
```bsl
ВЫБРАТЬ
  Контрагенты.Наименование КАК Наименование,
  Контрагенты.ТипКонтрагента КАК ТипКонтрагента,
  Контрагенты.ЮрЛицо КАК ЮрЛицо,
  Контрагенты.ЭтоГруппа КАК ЭтоГруппа
ИЗ
  Справочник.Контрагенты КАК Контрагенты
ГДЕ
  НЕ Контрагенты.ЭтоГруппа
```
В условии "Контрагенты.ЭтоГруппа = ЛОЖЬ" аналогично "НЕ Контрагенты.ЭтоГруппа"


### Проверка значения в списке
```bsl
ВЫБРАТЬ
  Сотрудники.Фамилия КАК Фамилия,
  Сотрудники.ДатаРождения КАК ДатаРождения,
  Сотрудники.ВидСотрудника КАК ВидСотрудника
ИЗ
  Справочник.Сотрудники КАК Сотрудники
ГДЕ
  Сотрудники.ВидСотрудника В (&ВидСотрудника)
```

### Проверка значения в списке по иерархии
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  Товары.Родитель КАК Родитель
ИЗ
  Справочник.Товары КАК Товары
ГДЕ
  НЕ Товары.ЭтоГруппа
  И Товары.Ссылка В ИЕРАРХИИ (&Группа)
```

### Отбор по периоду
```bsl
ВЫБРАТЬ
  ПродажаТовара.Дата КАК Дата,
  ПродажаТовара.Номер КАК Номер,
  ПродажаТовара.Контрагент КАК Контрагент,
  ПродажаТовара.Склад КАК Склад
ИЗ
  Документ.ПродажаТовара КАК ПродажаТовара
ГДЕ
  ПродажаТовара.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
```

### Отбор по ссылке
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка
ИЗ
  ЖурналДокументов.Товары КАК Товары
ГДЕ
  Товары.Ссылка ССЫЛКА Документ.ЗакупкаТовара
```

### Отбор по вхождению некоторого значения
```bsl
ВЫБРАТЬ
  Товары.Наименование КАК Наименование
ИЗ
  Справочник.Товары КАК Товары
ГДЕ
  Товары.Наименование ПОДОБНО "%антивирус%"
```

### Отбор на записи, содержащие цифры в наименовании
```bsl
ВЫБРАТЬ
  Товары.Наименование КАК Наименование
ИЗ
  Справочник.Товары КАК Товары
ГДЕ
  Товары.Наименование ПОДОБНО "%[0-9]%"
```

### Отбор на записи, в которых нет русских букв
```bsl
ВЫБРАТЬ
  Товары.Наименование КАК Наименование
ИЗ
  Справочник.Товары КАК Товары
ГДЕ
  Товары.Наименование ПОДОБНО "[^а-я]%"
```

### Отбор на записи, содержащие спецсимвол "!"
```bsl
ВЫБРАТЬ
  Товары.Наименование КАК Наименование
ИЗ
  Справочник.Товары КАК Товары
ГДЕ
  Товары.Наименование ПОДОБНО "%!_%" СПЕЦСИМВОЛ "!"
```

### Отбор по наименованию, в которой содержится строка "отвертка" 
```bsl
ВЫБРАТЬ
  Номенклатура.Наименование,
  Номенклатура.Код,
  Номенклатура.Услуга,
  Номенклатура.Качество,
  Номенклатура.Происхождение,
  Номенклатура.Артикул,
  Номенклатура.ОсновнаяСкидка
ИЗ
  Справочник.Номенклатура КАК Номенклатура
ГДЕ
  Номенклатура.Наименование ПОДОБНО "%отвертка%"
```

### Отбор записей с пустым значением
```bsl
ВЫБРАТЬ
  Контрагенты.Наименование КАК Наименование,
  Контрагенты.ЮрЛицо КАК ЮрЛицо,
  Контрагенты.ТипКонтрагента КАК ТипКонтрагента
ИЗ
  Справочник.Контрагенты КАК Контрагенты
ГДЕ
  Контрагенты.ТипКонтрагента ЕСТЬ NULL
```

### Получение элементов групп, подгрупп при этом сами группы не будут включены в выборку
```bsl
ВЫБРАТЬ
  Номенклатура.Наименование,
  Номенклатура.Код,
  Номенклатура.Услуга,
  Номенклатура.Качество,
  Номенклатура.Происхождение,
  Номенклатура.Артикул,
  Номенклатура.ОсновнаяСкидка
ИЗ
  Справочник.Номенклатура КАК Номенклатура
ГДЕ
  Номенклатура.Родитель В ИЕРАРХИИ(&Родитель)
  И Номенклатура.ЭтоГруппа = Ложь
```



## Упорядочивание

### Упорядочивание по дате и фамилии в порядке убывания
```bsl
ВЫБРАТЬ
  Сотрудники.Фамилия КАК Фамилия,
  Сотрудники.Имя КАК Имя,
  Сотрудники.ДатаРождения КАК ДатаРождения
ИЗ
  Справочник.Сотрудники КАК Сотрудники

УПОРЯДОЧИТЬ ПО
  ДатаРождения,
  Фамилия УБЫВ
```

### Упорядочивание по иерархии
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  Товары.Наименование КАК Наименование
ИЗ
  Справочник.Товары КАК Товары

УПОРЯДОЧИТЬ ПО
  Наименование ИЕРАРХИЯ
```


## Объединение запросов
```bsl
ВЫБРАТЬ
  ЗакупкаТовара.Ссылка КАК Ссылка
ИЗ
  Документ.ЗакупкаТовара КАК ЗакупкаТовара

ОБЪЕДИНИТЬ

ВЫБРАТЬ
  ПродажаТовара.Ссылка
ИЗ
  Документ.ПродажаТовара КАК ПродажаТовара
```

### Упорядочивание в объединенной таблице
```bsl
ВЫБРАТЬ
  ЗакупкаТовара.Ссылка КАК Ссылка,
  ЗакупкаТовара.Дата КАК Дата
ИЗ
  Документ.ЗакупкаТовара КАК ЗакупкаТовара

ОБЪЕДИНИТЬ

ВЫБРАТЬ
  ПродажаТовара.Ссылка,
  ПродажаТовара.Дата
ИЗ
  Документ.ПродажаТовара КАК ПродажаТовара

УПОРЯДОЧИТЬ ПО
  Дата
```

Упорядочивание происходит по результату объединения


### Значение NULL в объединяемой таблице
```bsl
ВЫБРАТЬ
  ЗакупкаТовара.Ссылка КАК Ссылка,
  ЗакупкаТовара.Дата КАК Дата,
  ЗакупкаТовара.Склад КАК Склад
ИЗ
  Документ.ЗакупкаТовара КАК ЗакупкаТовара

ОБЪЕДИНИТЬ

ВЫБРАТЬ
  ПродажаТовара.Ссылка,
  ПродажаТовара.Дата,
  NULL
ИЗ
  Документ.ПродажаТовара КАК ПродажаТовара

УПОРЯДОЧИТЬ ПО
  Дата
```
При добавлении элемента, которого нет в другой объединяемой таблице, конструктор присваивает ему значение NULL.


### Объединение с использованием пустой таблицы
```bsl
ВЫБРАТЬ
  ЗакупкаТовара.Ссылка КАК Ссылка,
  ЗакупкаТовара.Дата КАК Дата,
  ЗакупкаТовара.Номер КАК Номер,
  ЗакупкаТовара.Товары.(
    Товар КАК Товар,
    Количество КАК Количество,
    Цена КАК Цена,
    Сумма КАК Сумма
  ) КАК Товары
ИЗ
  Документ.ЗакупкаТовара КАК ЗакупкаТовара

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  ПродажаТовара.Ссылка,
  ПродажаТовара.Дата,
  ПродажаТовара.Номер,
  ПУСТАЯТАБЛИЦА.(, , , )
ИЗ
  Документ.ПродажаТовара КАК ПродажаТовара
```



## Соединение таблиц

### Внутреннее соединение с множественным условием
```bsl
ВЫБРАТЬ
  ЗакупкаТовара.Ссылка КАК Ссылка,
  ПродажаТовара.Ссылка КАК Ссылка1,
  ЗакупкаТовара.Склад КАК Склад,
  ПродажаТовара.Склад КАК Склад1
ИЗ
  Документ.ЗакупкаТовара КАК ЗакупкаТовара
    ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПродажаТовара КАК ПродажаТовара
    ПО (ЗакупкаТовара.Контрагент = ПродажаТовара.Контрагент
        И ЗакупкаТовара.Склад = ПродажаТовара.Склад)
```

Конструктор запросов не позволяет создавать правый тип соединения, приводя его к левому.



## Группировки

### Подсчет суммы с группировкой по двум полям
```bsl
ВЫБРАТЬ
  ОказаниеУслугУслуги.Услуга КАК Услуга,
  СУММА(ОказаниеУслугУслуги.Сумма) КАК Сумма,
  ОказаниеУслугУслуги.Ссылка.Контрагент КАК Контрагент
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги

СГРУППИРОВАТЬ ПО
  ОказаниеУслугУслуги.Услуга,
  ОказаниеУслугУслуги.Ссылка.Контрагент
```

### Подсчет суммы за все оказанные услуги
```bsl
ВЫБРАТЬ
  СУММА(ОказаниеУслугУслуги.Сумма) КАК Сумма
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги
```

### Подсчет суммы и количества оказываемых услуг
```bsl
ВЫБРАТЬ
  ОказаниеУслугУслуги.Услуга КАК Услуга,
  СУММА(ОказаниеУслугУслуги.Сумма) КАК Сумма,
  ОказаниеУслугУслуги.Ссылка.Контрагент КАК Контрагент,
  КОЛИЧЕСТВО(ОказаниеУслугУслуги.Услуга) КАК КоличествоУслуг
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги

СГРУППИРОВАТЬ ПО
  ОказаниеУслугУслуги.Услуга,
  ОказаниеУслугУслуги.Ссылка.Контрагент
```

### Отображение количества строк таблицы и количества оказанных услуг
```bsl
ВЫБРАТЬ
  КОЛИЧЕСТВО(*) КАК КоличествоСтрок,
  КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОказаниеУслугУслуги.Услуга) КАК Услуга
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги
```

### Подсчет среднего, максимального и минимального значения услуги
```bsl
ВЫБРАТЬ
  ОказаниеУслугУслуги.Услуга КАК Услуга,
  СРЕДНЕЕ(ОказаниеУслугУслуги.Сумма) КАК Сумма,
  МАКСИМУМ(ОказаниеУслугУслуги.Сумма) КАК Сумма1,
  МИНИМУМ(ОказаниеУслугУслуги.Сумма) КАК Сумма2
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги

СГРУППИРОВАТЬ ПО
  ОказаниеУслугУслуги.Услуга
```

### Задание условий для группировок
```
ВЫБРАТЬ
  ОказаниеУслугУслуги.Услуга КАК Услуга,
  СРЕДНЕЕ(ОказаниеУслугУслуги.Сумма) КАК Сумма
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги

СГРУППИРОВАТЬ ПО
  ОказаниеУслугУслуги.Услуга

ИМЕЮЩИЕ
  СУММА(ОказаниеУслугУслуги.Сумма) >= 5000 И
  КОЛИЧЕСТВО(ОказаниеУслугУслуги.Услуга) > 1
```


## Вложенные запросы
```bsl
ВЫБРАТЬ
  ЗакупкиПродажи.Товар КАК Товар,
  СУММА(ЗакупкиПродажи.Сумма) КАК Сумма
ИЗ
  (ВЫБРАТЬ
    Закупка.Товар КАК Товар,
    СУММА(-Закупка.Сумма) КАК Сумма
  ИЗ
    Документ.ЗакупкаТовара.Товары КАК Закупка
  
  СГРУППИРОВАТЬ ПО
    Закупка.Товар
  
  ОБЪЕДИНИТЬ ВСЕ
  
  ВЫБРАТЬ
    Продажа.Товар,
    СУММА(Продажа.Сумма)
  ИЗ
    Документ.ПродажаТовара.Товары КАК Продажа
  
  СГРУППИРОВАТЬ ПО
    Продажа.Товар) КАК ЗакупкиПродажи

СГРУППИРОВАТЬ ПО
  ЗакупкиПродажи.Товар
```


## Выражения в языке запросов

### Добавление констант в запрос
```bsl
ВЫБРАТЬ
  ЗакупкаТовара.Контрагент КАК Контрагент,
  ЗакупкаТовара.Контрагент.ЮрЛицо КАК КонтрагентЮрЛицо,
  1000 КАК Число,
  "Строка" КАК Строка,
  ИСТИНА КАК Булево,
  &Параметр КАК Параметр
ИЗ
  Документ.ЗакупкаТовара КАК ЗакупкаТовара
```

### Добавляем поле в таблицу, принимающая одно из трех значений в зависимости от условия
```bsl
ВЫБРАТЬ
  Услуги.Услуга КАК Услуга,
  Услуги.Сумма КАК Сумма,
  ВЫБОР
    КОГДА Услуги.Сумма > 5000
      ТОГДА "Крупная"
    КОГДА Услуги.Сумма > 1500
      ТОГДА "Средняя"
    ИНАЧЕ "Незначительная"
  КОНЕЦ КАК ВидУслуги
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК Услуги
```

### Пример запроса с выводом значения, если оно соотвествует параметру
```bsl
ВЫБРАТЬ
  Услуги.Услуга КАК Услуга,
  Услуги.Сумма КАК Сумма,
  ВЫБОР
    КОГДА Услуги.Сумма > 5000
      ТОГДА "Крупная"
    КОГДА Услуги.Сумма > 1500
      ТОГДА "Средняя"
    ИНАЧЕ "Незначительная"
  КОНЕЦ КАК ВидУслуги
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК Услуги
ГДЕ
  ВЫБОР
      КОГДА Услуги.Сумма < 2000
        ТОГДА Услуги.Сотрудник = &Сотрудник
      ИНАЧЕ ИСТИНА
    КОНЕЦ
```


## Встроенные функции

### Получение части строки
```bsl
ВЫБРАТЬ
  Товары.Наименование КАК Наименование,
  ПОДСТРОКА(Товары.Наименование, 3, 10) КАК Поле1
ИЗ
  Справочник.Товары КАК Товары
```

### Получение ссылки в виде строки
```bsl
ВЫБРАТЬ
  ПРЕДСТАВЛЕНИЕ(Контрагенты.Ссылка) КАК Название
ИЗ
  Справочник.Контрагенты КАК Контрагенты
```

### Проверяем заполнено ли значение, если нет, заполняем текстом
```bsl
ВЫБРАТЬ
  Услуги.Ссылка КАК Услуга,
  ЕСТЬNULL(ОказаниеУслуг.Ссылка, "Услуга не оказывается") КАК СсылкаНаДокумент,
  ЕСТЬNULL(ОказаниеУслуг.Услуга, "Услуга не оказывается") КАК УслугаНаДокумент
ИЗ
  Справочник.Услуги КАК Услуги
    ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслуг
    ПО (Услуги.Ссылка = ОказаниеУслуг.Услуга)
```

### Получение типа значения поля
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  ТИПЗНАЧЕНИЯ(Товары.Ссылка) КАК Тип
ИЗ
  ЖурналДокументов.Товары КАК Товары
ГДЕ
  ТИПЗНАЧЕНИЯ(Товары.Ссылка) = ТИП(Документ.ЗакупкаТовара)
```

### Получение даты
```bsl
ВЫБРАТЬ
  ДАТАВРЕМЯ(2021, 10, 30, 15, 45, 34) КАК Дата
```

### Задание пустой даты

```bsl
ВЫБРАТЬ
  ДАТАВРЕМЯ(1, 1, 1) КАК Дата
```

### Получение полей из стандартных функций с датами  
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  ГОД(Товары.Дата) КАК Год,
  МЕСЯЦ(Товары.Дата) КАК Месяц,
  КВАРТАЛ(Товары.Дата) КАК Квартал,
  ДЕНЬ(Товары.Дата) КАК День,
  ЧАС(Товары.Дата) КАК Час,
  МИНУТА(Товары.Дата) КАК Минута,
  СЕКУНДА(Товары.Дата) КАК Секунда,
  НЕДЕЛЯ(Товары.Дата) КАК НомерНедели
ИЗ
  ЖурналДокументов.Товары КАК Товары
```

### Определяем день недели 
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  Товары.Дата КАК Дата,
  ВЫБОР
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 1
      ТОГДА "понедельник"
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 2
      ТОГДА "вторник"
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 3
      ТОГДА "среда"
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 4
      ТОГДА "четверг"
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 5
      ТОГДА "пятница"
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 6
      ТОГДА "суббота"
    КОГДА ДЕНЬНЕДЕЛИ(Товары.Дата) = 7
      ТОГДА "воскресенье"
  КОНЕЦ КАК ДеньНедели
ИЗ
  ЖурналДокументов.Товары КАК Товары
```

### Определяем начало, конец временного интервала 
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  Товары.Дата КАК Дата,
  НАЧАЛОПЕРИОДА(Товары.Дата, ГОД) КАК НачалоГода,
  КОНЕЦПЕРИОДА(Товары.Дата, ГОД) КАК КонецГода
ИЗ
  ЖурналДокументов.Товары КАК Товары
```

### Добавляем значение к дате
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  Товары.Дата КАК Дата,
  ДОБАВИТЬКДАТЕ(Товары.Дата, ГОД, 1) КАК Поле1
ИЗ
  ЖурналДокументов.Товары КАК Товары
```

### Определяем разность дат
```bsl
ВЫБРАТЬ
  Товары.Ссылка КАК Ссылка,
  РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(Товары.Дата, МЕСЯЦ), Товары.Дата, ДЕНЬ) КАК Разность
ИЗ
  ЖурналДокументов.Товары КАК Товары
```

### Примеры использования функции "ЗНАЧЕНИЕ"
```bsl
ВЫБРАТЬ
  Контрагенты.Ссылка КАК Ссылка,
  Контрагенты.ЮрЛицо КАК ЮрЛицо
ИЗ
  Справочник.Контрагенты КАК Контрагенты
ГДЕ
  Контрагенты.ЮрЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрЛицоКлиента.ИП)
```

```bsl
ВЫБРАТЬ
  Сотрудники.Ссылка КАК Ссылка,
  Сотрудники.ВидСотрудника КАК ВидСотрудника
ИЗ
  Справочник.Сотрудники КАК Сотрудники
ГДЕ
  Сотрудники.ВидСотрудника = ЗНАЧЕНИЕ(Справочник.ВидыСотрудников.ПустаяСсылка)
```

### Примеры использования функции "ВЫРАЗИТЬ"

При работе с составными типами оптимизирует запрос

```bsl
ВЫБРАТЬ
  ВЫБОР
    КОГДА Товары.Ссылка ССЫЛКА Документ.ПродажаТовара
      ТОГДА ВЫРАЗИТЬ(Товары.Ссылка КАК Документ.ПродажаТовара).Контрагент
    КОГДА Товары.Ссылка ССЫЛКА Документ.ЗакупкаТовара
      ТОГДА ВЫРАЗИТЬ(Товары.Ссылка КАК Документ.ЗакупкаТовара).Контрагент
  КОНЕЦ КАК Контрагент
ИЗ
  ЖурналДокументов.Товары КАК Товары
ГДЕ
  Товары.Ссылка ССЫЛКА Документ.ЗакупкаТовара
```

```bsl
ВЫБРАТЬ
  Контрагенты.Ссылка КАК Ссылка,
  ВЫРАЗИТЬ(Контрагенты.НаименованиеПолное КАК СТРОКА(5)) КАК Поле1
ИЗ
  Справочник.Контрагенты КАК Контрагенты
```

```bsl
ВЫБРАТЬ
  100 / 7 КАК Поле1,
  ВЫРАЗИТЬ(100 / 7 КАК ЧИСЛО(10, 6)) КАК Поле2
```



## Использование итогов
```bsl
ВЫБРАТЬ
  ОказаниеУслугУслуги.Ссылка.Контрагент КАК Контрагент,
  ОказаниеУслугУслуги.Услуга КАК Услуга,
  ОказаниеУслугУслуги.Сумма КАК Сумма,
  ОказаниеУслугУслуги.Ссылка КАК Ссылка
ИЗ
  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги
ИТОГИ
  СУММА(Сумма)
ПО
  Контрагент,
  Услуга
```

```bsl
ВЫБРАТЬ
  ЗакупкаТовараТовары.Товар КАК Товар,
  ЗакупкаТовараТовары.Количество КАК Количество,
  ЗакупкаТовараТовары.Ссылка КАК Ссылка
ИЗ
  Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
ИТОГИ
  СУММА(Количество)
ПО
  Товар ИЕРАРХИЯ
```

```bsl
ВЫБРАТЬ
  ЗакупкаТовараТовары.Товар КАК Товар,
  ЗакупкаТовараТовары.Количество КАК Количество,
  ЗакупкаТовараТовары.Ссылка КАК Ссылка
ИЗ
  Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
ИТОГИ
  СУММА(Количество)
ПО
  Товар ТОЛЬКО ИЕРАРХИЯ
```


## Получение линейной выборки из запроса


#### Модуль формы внешней обработки

```bsl
&НаСервере
Процедура ЗаполнитьНаСервере(ПустыеДанные)
  Запрос = Новый Запрос();
  Запрос.Текст = "
 |ВЫБРАТЬ
 |  Контрагенты.Ссылка КАК Контрагент,
 |  Контрагенты.ЮрЛицо КАК ЮрЛицо,
 |  Контрагенты.ТипКонтрагента КАК ТипКонтрагента,
 |  Контрагенты.Адрес КАК Адрес,
 |  Контрагенты.Телефон КАК Телефон
 |ИЗ
 |  Справочник.Контрагенты КАК Контрагенты
 |ГДЕ
 |  Контрагенты.ЮрЛицо = &ЮрЛицо
 |  И Контрагенты.ТипКонтрагента = &ТипКонтрагента";
  
  Запрос.УстановитьПараметр("ЮрЛицо", ЮрЛицо);
  Запрос.УстановитьПараметр("ТипКонтрагента", ТипКонтрагента);
                                            
  Результат = Запрос.Выполнить();
  Выборка = Результат.Выбрать();
  
  //Пока Выборка.Следующий() Цикл
  //  НоваяСтрока = ТаблицаКонтрагентов.Добавить();
  //  
  //  // 1 вариант заполнени
  //  НоваяСтрока.Контрагент = Выборка.Контрагент;
  //  НоваяСтрока.ЮрЛицо = Выборка.ЮрЛицо;
  //  НоваяСтрока.ТипКонтрагента = Выборка.ТипКонтрагента;
  //  НоваяСтрока.Адрес = Выборка.Адрес;
  //  НоваяСтрока.Телефон = Выборка.Телефон;    

  //  // 2 вариант заполнения(работает когда значения совпадают, однако выполняется дольше)   
  //  ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);                                         
  //  
  //КонецЦикла;
  
  Если Результат.Пустой() Тогда
    ПустыеДанные = Истина;
    Возврат;
  КонецЕсли;
  
  // 3 вариант заполнения(работает медленее 2 варианта)
  ТаблицаКонтрагентов.Загрузить(Результат.Выгрузить());
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ПустыеДанные = Ложь;
  ЗаполнитьНаСервере(ПустыеДанные);
  Если ПустыеДанные Тогда
    ПоказатьПредупреждение(ОписаниеОповещенияОЗакрытии, "Нет данных по текущим запросам");
  КонецЕсли;
КонецПроцедуры
```


### Выборка из вложенной таблицы

#### Модуль формы внешней обработки 

```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()

  Запрос = Новый Запрос;
  Запрос.Текст = "
  |ВЫБРАТЬ
  |  ПродажаТовара.Ссылка КАК Ссылка,
  |  ПродажаТовара.Товары.(
  |    Товар КАК Товар,
  |    Количество КАК Количество
  |  ) КАК Товары
  |ИЗ
  |  Документ.ПродажаТовара КАК ПродажаТовара";
  
  Выборка = Запрос.Выполнить().Выбрать();
  Пока Выборка.Следующий() Цикл

    ВерхняяСтрокаДерева = ПродажаТовара.ПолучитьЭлементы();
    НоваяСтрока = ВерхняяСтрокаДерева.Добавить();
    НоваяСтрока.Ссылка = Выборка.Ссылка;
    
    ТаблТовары = Выборка.Товары;
    ВыборкаТоваров = ТаблТовары.Выбрать();
    
    Пока ВыборкаТоваров.Следующий() Цикл
      ДочерняяСтрокаДерева = НоваяСтрока.ПолучитьЭлементы();
      СтрокаТовары = ДочерняяСтрокаДерева.Добавить();

      СтрокаТовары.Ссылка = ВыборкаТоваров.Товар;
      СтрокаТовары.Количество = ВыборкаТоваров.Количество;
    КонецЦикла;    
  КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```


## Обход результатов запроса

### Обход результатов запроса по группировкам

```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()

  Запрос = Новый Запрос;
  Запрос.Текст = "
  |ВЫБРАТЬ
  |  ОказаниеУслугУслуги.Ссылка.Контрагент КАК Контрагент,
  |  ОказаниеУслугУслуги.Сотрудник КАК Сотрудник,
  |  ОказаниеУслугУслуги.Услуга КАК Услуга,
  |  ОказаниеУслугУслуги.Сумма КАК Сумма
  |ИЗ
  |  Документ.ОказаниеУслуг.Услуги КАК ОказаниеУслугУслуги
  |ИТОГИ
  |  СУММА(Сумма)
  |ПО
  |  Контрагент,
  |  Сотрудник";
  
  Результат = Запрос.Выполнить();
  ВыборкаКлиент = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Клиент");
  Пока ВыборкаКлиент.Следующий() Цикл
    ВерхняяСтрокаДерева = ОказаниеУслуг.ПолучитьЭлементы();
    НоваяСтрока = ВерхняяСтрокаДерева.Добавить();   
    НоваяСтрока.Клиент = ВыборкаКлиент.Клиент;
    НоваяСтрока.Сумма = ВыборкаКлиент.Сумма;
    
    ВыборкаСотрудник = ВыборкаКлиент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Сотрудник");
    Пока ВыборкаСотрудник.Следующий() Цикл
      ВтораСтрокаДерева = НоваяСтрока.ПолучитьЭлементы();
      СтрокаСотрудников = ВтораСтрокаДерева.Добавить();
      СтрокаСотрудников.Клиент = ВыборкаСотрудник.Сотрудник;
      СтрокаСотрудников.Сумма = ВыборкаСотрудник.Сумма;
      
      ВыборкаУслуг = ВыборкаСотрудник.Выбрать(ОбходРезультатаЗапроса.Прямой);
      Пока ВыборкаУслуг.Следующий() Цикл 
        ТретьяСтрокаДерева = СтрокаСотрудников.ПолучитьЭлементы();
        СтрокаУслуг = ТретьяСтрокаДерева.Добавить();
        СтрокаУслуг.Клиент = ВыборкаУслуг.Услуга;
        СтрокаУслуг.Сумма = ВыборкаУслуг.Сумма;     
      КонецЦикла;
    КонецЦикла;
  КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```

### Обход результатов запроса по группировкам с иерархией
```bsl
&НаСервере 
Процедура ВывестиРекурсивно(Выборка, СтрокаДерева)
  Пока Выборка.Следующий() Цикл
    Если Выборка.Группировка() = "Товара" Тогда

      НоваяСтрока = СтрокаДерева.Добавить();
      ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
      
      НоваяВыборка = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Товар");
      Если НоваяВыборка.Количество() = 0 Тогда        
        НоваяВыборка = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Контрагент");        
      КонецЕсли;
      
      ДочерняяСтрокаДерева = НоваяСтрока.ПолучитьЭлементы();      
      ВывестиРекурсивно(НоваяВыборка, ДочерняяСтрокаДерева);
      
    ИначеЕсли Выборка.Группировка() = "Контрагент" Тогда      
      НоваяСтрока = СтрокаДерева.Добавить();
      ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
      НоваяСтрока.Товар = Выборка.Контрагент;
      
      НоваяВыборка = Выборка.Выбрать(ОбходРезультатаЗапроса.Прямой);
      ДочерняяСтрокаДерева = НоваяСтрока.ПолучитьЭлементы();      
      ВывестиРекурсивно(НоваяСтрока, ДочерняяСтрокаДерева);     
      
    Иначе     
      НоваяСтрока = СтрокаДерева.Добавить();
      ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);     
      НоваяСтрока.Товар = Выборка.Товар;
      НоваяСтрока.Документ = Выборка.Ссылка;    
    КонецЕсли;
    
  КонецЦикла;
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНаСервере()
  Запрос = Новый Запрос;
  Запрос.Текст = "
  |ВЫБРАТЬ
  |  ПродажаТовараТовары.Ссылка.Контрагент КАК Контрагент,
  |  ПродажаТовараТовары.Товар КАК Товар,
  |  ПродажаТовараТовары.Ссылка КАК Ссылка,
  |  ПродажаТовараТовары.Количество КАК Количество,
  |  ПродажаТовараТовары.Сумма КАК Сумма
  |ИЗ
  |  Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
  |ИТОГИ
  |  СУММА(Количество),
  |  СУММА(Сумма)
  |ПО
  |  Товар ТОЛЬКО ИЕРАРХИЯ,
  |  Контрагент";
  
  Результат = Запрос.Выполнить();
  Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Товар");
  ВерхняяСтрокаДерева = ПродажаТовара.ПолучитьЭлементы();
  ВывестиРекурсивно(Выборка, ВерхняяСтрокаДерева);
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```


## Использование временных таблиц в запросе

### 1 Вариант
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
  
  Запрос = Новый Запрос;
  Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
  Запрос.Текст = "
  |ВЫБРАТЬ
  | Закупка.Товар КАК Товар,
  | СУММА(-Закупка.Сумма) КАК Сумма
  |ПОМЕСТИТЬ втЗакупкиПродажи
  |ИЗ
  | Документ.ЗакупкаТовара.Товары КАК Закупка
  |
  |СГРУППИРОВАТЬ ПО
  | Закупка.Товар
  |
  |ОБЪЕДИНИТЬ ВСЕ
  |
  |ВЫБРАТЬ
  | ПродажаТовараТовары.Товар,
  | СУММА(ПродажаТовараТовары.Сумма)
  |ИЗ
  | Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
  |
  |СГРУППИРОВАТЬ ПО
  | ПродажаТовараТовары.Товар
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |ВЫБРАТЬ
  | втЗакупкиПродажи.Товар КАК Товар,
  | СУММА(втЗакупкиПродажи.Сумма) КАК Сумма
  |ИЗ
  | втЗакупкиПродажи КАК втЗакупкиПродажи
  |
  |СГРУППИРОВАТЬ ПО
  | втЗакупкиПродажи.Товар";
  
  Результат = Запрос.Выполнить();
  Выборка = Результат.Выбрать();
  Пока Выборка.Следующий() Цикл 
    НоваяСтрока = ПокупкиПродажи.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
  КонецЦикла;
  
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```

### 2 Вариант
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
  
  Запрос = Новый Запрос;
  Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
  Запрос.Текст = "ВЫБРАТЬ
  |    Закупка.Товар Как Товар,
  | СУММА(-Закупка.Сумма) КАК Сумма
  |ПОМЕСТИТЬ втЗакупкиПродажи
  |ИЗ
  | Документ.ЗакупкаТовара.Товары КАК Закупка
  |
  |СГРУППИРОВАТЬ ПО
  | Закупка.Товар
  |Объединить все
  |
  |выбрать
  |ПродажаТовараТовары.Товар
  |СУММА(ПродажаТовараТовары.Сумма)
  |ИЗ
  |Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
  |СГРУППИРОВАТЬ ПО
  |  ПродажаТовараТовары.Товар";  
  
  Запрос.Выполнить();
  
  Запрос2 = Новый Запрос;
  Запрос2.Текст = "ВЫБРАТЬ
  | втЗакупкиПродажи.Товар КАК Товар,
  | СУММА(втЗакупкиПродажи.Сумма) КАК Сумма
  |ИЗ
  | втЗакупкиПродажи КАК втЗакупкиПродажи
  |
  |СГРУППИРОВАТЬ ПО
  | втЗакупкиПродажи.Товар";
  
  Запрос2.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
  
  Результат = Запрос2.Выполнить();
  Выборка = Результат.Выбрать();
  Пока Выборка.Следующий() Цикл 
    НоваяСтрока = ПокупкиПродажи.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);   
  КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```


### Удаление временной таблицы
```bsl
Запрос3 = Новый Запрос;
Запрос3.Текст = "УНИЧТОЖИТЬ втЗакупкиПродажи";
Запрос3.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
Запрос3.Выполнить();
```

Также после вызова данного метода, временная таблица будет удалена.
```bsl
МенеджерВременныхТаблиц.Закрыть();
```


### Пример использования временных таблиц
```bsl
ВЫБРАТЬ
  Номенклатура.Ссылка КАК Ссылка
ПОМЕСТИТЬ ВТ_Номенклатура
ИЗ
  Справочник.Номенклатура КАК Номенклатура
ГДЕ
  Номенклатура.ЭтоГруппа = ЛОЖЬ
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
  ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
ПОМЕСТИТЬ ВТ_Цены
ИЗ
  РегистрСведений.ЦеныНоменклатуры.СрезПоследних КАК ЦеныНоменклатурыСрезПоследних
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  ОстаткиТоваровОстатки.Номенклатура КАК Номенклатура,
  ОстаткиТоваровОстатки.КоличествоОстаток КАК КоличествоОстаток
ПОМЕСТИТЬ ВТ_Остатки
ИЗ
  РегистрНакопления.ОстаткиТоваров.Остатки КАК ОстаткиТоваровОстатки
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  ВТ_Номенклатура.Ссылка КАК Номенклатура,
  ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК Остаток,
  ЕСТЬNULL(ВТ_Цены.Цена, 0) КАК Цена
ИЗ
  ВТ_Номенклатура КАК ВТ_Номенклатура
    ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Цены КАК ВТ_Цены
    ПО (ВТ_Номенклатура.Ссылка = ВТ_Цены.Номенклатура)
    ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
    ПО (ВТ_Номенклатура.Ссылка = ВТ_Остатки.Номенклатура)
```


## Работа с пакетными запросами
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  Запрос = Новый Запрос;
  Запрос.Текст = "
  |ВЫБРАТЬ
  | ЗакупкаТовара.Ссылка КАК Документ
  |ИЗ
  | Документ.ЗакупкаТовара КАК ЗакупкаТовара
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |ВЫБРАТЬ
  | ПродажаТовара.Ссылка КАК Документ
  |ИЗ
  | Документ.ПродажаТовара КАК ПродажаТовара";
  
  Результаты = Запрос.ВыполнитьПакет();
  
  РезультатЗакупка = Результаты[0];
  РезультатПродажа = Результаты[1];
  
  ВыборкаЗакупка = РезультатЗакупка.Выбрать();  
  Пока ВыборкаЗакупка.Следующий() Цикл
    НоваяСтрокаЗакупка = ЗакупкаТовара.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрокаЗакупка, ВыборкаЗакупка);
  КонецЦикла;
  
  ВыборкаПродажа = РезультатПродажа.Выбрать();  
  Пока ВыборкаПродажа.Следующий() Цикл
    НоваяСтрокаПродажа = ПродажаТовара.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрокаПродажа, ВыборкаПродажа);
  КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```


```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  Запрос = Новый Запрос;
  Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
  Запрос.Текст = "ВЫБРАТЬ
  | ЗакупкаТовараТовары.Товар КАК Товар,
  | СУММА(-ЗакупкаТовараТовары.Количество) КАК Количество,
  | СУММА(-ЗакупкаТовараТовары.Сумма) КАК Сумма
  |ПОМЕСТИТЬ втЗакупкиПродажи
  |ИЗ
  | Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
  |
  |СГРУППИРОВАТЬ ПО
  | ЗакупкаТовараТовары.Товар
  |
  |ОБЪЕДИНИТЬ ВСЕ
  |
  |ВЫБРАТЬ
  | ПродажаТовараТовары.Товар,
  | СУММА(ПродажаТовараТовары.Количество),
  | СУММА(ПродажаТовараТовары.Сумма)
  |ИЗ
  | Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
  |
  |СГРУППИРОВАТЬ ПО
  | ПродажаТовараТовары.Товар
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |ВЫБРАТЬ
  | втЗакупкиПродажи.Товар КАК Товар,
  | СУММА(втЗакупкиПродажи.Сумма) КАК Сумма
  |ИЗ
  | втЗакупкиПродажи КАК втЗакупкиПродажи
  |
  |СГРУППИРОВАТЬ ПО
  | втЗакупкиПродажи.Товар
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |ВЫБРАТЬ
  | втЗакупкиПродажи.Товар КАК Товар,
  | СУММА(втЗакупкиПродажи.Количество) КАК Количество
  |ИЗ
  | втЗакупкиПродажи КАК втЗакупкиПродажи
  |
  |СГРУППИРОВАТЬ ПО
  | втЗакупкиПродажи.Товар
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |УНИЧТОЖИТЬ втЗакупкиПродажи";                                          
  
  МассивРезультатов = Запрос.Выполнить();
  РезультатСумма = МассивРезультатов[1];
  РезультатКоличество = МассивРезультатов[2];
  
  ВыборкаСумма = РезультатСумма.Выбрать();
  Пока ВыборкаСумма.Следующий() Цикл
    НоваяСтрока = ПокупкиПродажиСумма.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСумма);
  КонецЦикла;
  
  ВыборкаКоличество = РезультатКоличество.Выбрать();
  Пока ВыборкаКоличество.Следующий() Цикл
    НоваяСтрокаКоличество = ПокупкаПродажиКоличество.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрокаКоличество, ВыборкаКоличество);
  КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```



## Расширенное применение оператора множественного выбора (В)

### Передача в запрос массива и списка значений
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  Запрос = Новый Запрос;
  Запрос.Текст = "ВЫБРАТЬ
  | Контрагенты.Ссылка КАК Контрагент,
  | Контрагенты.ТипКонтрагента КАК ТипКонтрагента,
  | Контрагенты.ЮрЛицо КАК ЮрЛицо
  |ИЗ
  | Справочник.Контрагенты КАК Контрагенты
  |ГДЕ
  | Контрагенты.ТипКонтрагента В(&ТипКонтрагента)
  | И Контрагенты.ЮрЛицо В(&ЮрЛицо)";
  
  МассивТиповКонтрагента = ТипКонтрагента.ВыгрузитьЗначения();
  Запрос.УстановитьПараметр("ТипКонтрагента", МассивТиповКонтрагента);
  Запрос.УстановитьПараметр("ЮрЛицо", ЮрЛицо);
  
  Выборка = Запрос.Выполнить().Выбрать();
  Пока Выборка.Следующий() Цикл
    НоваяСтрока = СписокКонтрагентов.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
  КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```

### Использование "В" с вложенным запросом
```bsl
ВЫБРАТЬ
  ЗакупкаТовараТовары.Ссылка КАК Ссылка,
  ЗакупкаТовараТовары.Товар КАК Товар,
  ЗакупкаТовараТовары.Количество КАК Количество
ИЗ
  Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
ГДЕ
  ЗакупкаТовараТовары.Товар В
      (ВЫБРАТЬ
        ПродажаТовара.Товар КАК Товар
      ИЗ
        Документ.ПродажаТовара.Товары КАК ПродажаТовара
      СГРУППИРОВАТЬ ПО
        ПродажаТовара.Товар
      ИМЕЮЩИЕ
        СУММА(ПродажаТовара.Количество) > 1)
```

### Использование "В" с несколькими полями вложенного запроса
```bsl
ВЫБРАТЬ
  ЗакупкаТовараТовары.Ссылка КАК Ссылка,
  ЗакупкаТовараТовары.Товар КАК Товар,
  ЗакупкаТовараТовары.Количество КАК Количество
ИЗ
  Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
ГДЕ
  (ЗакупкаТовараТовары.Товар, ЗакупкаТовараТовары.Количество) В
      (ВЫБРАТЬ
        ПродажаТовара.Товар КАК Товар
        Сумма(ПродажаТовара.Количество) как Количество
      ИЗ
        Документ.ПродажаТовара.Товары КАК ПродажаТовара
      СГРУППИРОВАТЬ ПО
        ПродажаТовара.Товар
      ИМЕЮЩИЕ
        СУММА(ПродажаТовара.Количество) > 1)
```

### Использование "В" с временной таблицой
```bsl
ВЫБРАТЬ
  ПродажаТовара.Товар КАК Товар,
  СУММА(ПродажаТовара.Количество) КАК Количество
ПОМЕСТИТЬ втПродажи
ИЗ
  Документ.ПродажаТовара.Товары КАК ПродажаТовара

СГРУППИРОВАТЬ ПО
  ПродажаТовара.Товар

ИМЕЮЩИЕ
  СУММА(ПродажаТовара.Количество) > 1
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  ЗакупкаТовараТовары.Ссылка КАК Ссылка,
  ЗакупкаТовараТовары.Товар КАК Товар,
  ЗакупкаТовараТовары.Количество КАК Количество
ИЗ
  Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
ГДЕ
  (ЗакупкаТовараТовары.Товар, ЗакупкаТовараТовары.Количество) В
      (ВЫБРАТЬ
        ВТ.Товар,
        ВТ.Количество
      ИЗ
        втПродажи КАК ВТ)
```



## Передача параметров

### 1 способ передачи в параметр списка значений
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  Запрос = Новый Запрос;
  Запрос.Текст = "ВЫБРАТЬ
  | ЗакупкаТовараТовары.Ссылка КАК ДокументЗакупки,
  | ЗакупкаТовараТовары.Товар КАК Товар,
  | ЗакупкаТовараТовары.Количество КАК Количество
  |ИЗ
  | Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
  |ГДЕ
  | (ЗакупкаТовараТовары.Товар, ЗакупкаТовараТовары.Количество) В (&Параметр)";
  
  Запрос.УстановитьПараметр("Параметр", ТаблицаНоменклатуры.Выгрузить());
  
  Закупки.Очистить();
  
  Выборка = Запрос.Выполнить().Выбрать();
  Пока Выборка.Следующий() Цикл
    НоваяСтрока = Закупки.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
  КонецЦикла;   
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```


### 2 способ передачи в параметр списка значений
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  Запрос = Новый Запрос;
  Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
  Запрос.Текст = "ВЫБРАТЬ
  | Таблица.Товар КАК Товар,
  | Таблица.Количество КАК Количество
  |ПОМЕСТИТЬ втТовары
  |ИЗ
  | &Таблица КАК Таблица
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |ВЫБРАТЬ
  | ЗакупкаТовараТовары.Ссылка КАК ДокументЗакупки,
  | ЗакупкаТовараТовары.Товар КАК Товар,
  | ЗакупкаТовараТовары.Количество КАК Количество
  |ИЗ
  | Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
  |ГДЕ
  | (ЗакупкаТовараТовары.Товар, ЗакупкаТовараТовары.Количество) В
  |     (ВЫБРАТЬ
  |       вт.Товар,
  |       вт.Количество
  |     ИЗ
  |       втТовары КАК вт)";
  
  Запрос.УстановитьПараметр("Таблица", ТаблицаНоменклатуры.Выгрузить());
            
  Выборка = Запрос.Выполнить().Выбрать();
  Пока Выборка.Следующий() Цикл
    НоваяСтрока = Закупки.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
  КонецЦикла; 
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```


### Передача таблицы значений в параметр запроса

Необходимо также указать описание типов для таблицы значений
```bsl
&НаСервере
Процедура ЗаполнитьНаСервере()
  
  ТаблицаДляПараметра = Новый ТаблицаЗначений;
  ТаблицаДляПараметра.Колонки.Добавить("Товар", Новый ОписаниеТипов("Справочник.Ссылка.Товары"));
  ТаблицаДляПараметра.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
  
  НоваяСтТабл = ТаблицаДляПараметра.Добавить();
  НоваяСтТабл.Товар = Справочники.Товары.НайтиПоНаименованию("1С Бухгалтерия", Истина);
  НоваяСтТабл.Количество = 1;

  Запрос = Новый Запрос;
  Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
  Запрос.Текст = "ВЫБРАТЬ
  | Таблица.Товар КАК Товар,
  | Таблица.Количество КАК Количество
  |ПОМЕСТИТЬ втТовары
  |ИЗ
  | &Таблица КАК Таблица
  |;
  |
  |////////////////////////////////////////////////////////////////////////////////
  |ВЫБРАТЬ
  | ЗакупкаТовараТовары.Ссылка КАК ДокументЗакупки,
  | ЗакупкаТовараТовары.Товар КАК Товар,
  | ЗакупкаТовараТовары.Количество КАК Количество
  |ИЗ
  | Документ.ЗакупкаТовара.Товары КАК ЗакупкаТовараТовары
  |ГДЕ
  | (ЗакупкаТовараТовары.Товар, ЗакупкаТовараТовары.Количество) В
  |     (ВЫБРАТЬ
  |       вт.Товар,
  |       вт.Количество
  |     ИЗ
  |       втТовары КАК вт)";
  
  Запрос.УстановитьПараметр("Таблица", ТаблицаДляПараметра);
            
  Выборка = Запрос.Выполнить().Выбрать();
  Пока Выборка.Следующий() Цикл
    НоваяСтрока = Закупки.Добавить();
    ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
  КонецЦикла; 
КонецПроцедуры


&НаКлиенте
Процедура Заполнить(Команда)
  ЗаполнитьНаСервере();
КонецПроцедуры
```



## Работа с регистрами сведений

Виртуальная таблица существует на момент выполнения запроса

### СрезПервых с передачей параметра
```bsl
ВЫБРАТЬ
  ПроцентСотрудникаСрезПервых.Период КАК Период,
  ПроцентСотрудникаСрезПервых.Сотрудник КАК Сотрудник,
  ПроцентСотрудникаСрезПервых.Процент КАК Процент
ИЗ
  РегистрСведений.ПроцентСотрудника.СрезПервых(ДатаВремя(2015,12,31), ) КАК ПроцентСотрудникаСрезПервых
```

СрезПервых получает ближайшие даты после заданной даты

Если для виртуальной таблицы СрезПервых в качестве параметра не указывать дату в качестве параметра, то будут выведены первые записи таблицы.


### СрезПервых с передачей 2 параметров
```bsl
ВЫБРАТЬ
  ПроцентСотрудникаСрезПервых.Период КАК Период,
  ПроцентСотрудникаСрезПервых.Сотрудник КАК Сотрудник,
  ПроцентСотрудникаСрезПервых.Процент КАК Процент
ИЗ
  РегистрСведений.ПроцентСотрудника.СрезПервых(&Дата, Сотрудник = &Сотрудник) КАК ПроцентСотрудникаСрезПервых
```

### Срез последних
```bsl
ВЫБРАТЬ
  ПроцентСотрудникаСрезПоследних.Период КАК Период,
  ПроцентСотрудникаСрезПоследних.Сотрудник КАК Сотрудник,
  ПроцентСотрудникаСрезПоследних.Процент КАК Процент
ИЗ
  РегистрСведений.ПроцентСотрудника.СрезПоследних(ДатаВремя(2015,12,31), ) КАК ПроцентСотрудникаСрезПоследних
```

СрезПоследних возвращает ближайшие даты, которые идут перед заданной даты.

Если в качестве параметра не указывать дату в качестве параметра виртуальной таблицы СрезПоследних, то будут выведены последние записи таблицы.



## Работа с регистрами накопления

У регистров накопления нет реквизитов.

Чтобы можно было выбрать период у регистра накопления, необходимо в параметрах виртуальной таблицы указать периодичность, например год.

```bsl
ВЫБРАТЬ
  ОказаниеУслугОбороты.Период КАК Период,
  ОказаниеУслугОбороты.Сотрудник КАК Сотрудник,
  ОказаниеУслугОбороты.СуммаОборот КАК СуммаОборот
ИЗ
  РегистрНакопления.ОказаниеУслуг.Обороты(, , Год, ) КАК ОказаниеУслугОбороты
```

Периодичность может принимать следующие значения:

- Период (Выбор данных за весь период)
```bsl
ВЫБРАТЬ
  ОказаниеУслугОбороты.Сотрудник КАК Сотрудник,
  ОказаниеУслугОбороты.СуммаОборот КАК СуммаОборот
ИЗ
  РегистрНакопления.ОказаниеУслуг.Обороты(НачалоПериода(ДатаВремя(2015,9,1), День), КонецПериода(ДатаВремя(2015, 9, 30), День), , ) КАК ОказаниеУслугОбороты
```

- Запись (Содержит номер строки и регистратор)
```bsl
ВЫБРАТЬ
  ОказаниеУслугОбороты.Период КАК Период,
  ОказаниеУслугОбороты.Регистратор КАК Регистратор,
  ОказаниеУслугОбороты.НомерСтроки КАК НомерСтроки,
  ОказаниеУслугОбороты.Клиент КАК Клиент,
  ОказаниеУслугОбороты.Услуга КАК Услуга,
  ОказаниеУслугОбороты.Сотрудник КАК Сотрудник,
  ОказаниеУслугОбороты.СуммаОборот КАК СуммаОборот
ИЗ
  РегистрНакопления.ОказаниеУслуг.Обороты(, , Запись, ) КАК ОказаниеУслугОбороты
```

- Регистратор(Содержит регистратор)
```bsl
ВЫБРАТЬ
  ОказаниеУслугОбороты.Период КАК Период,
  ОказаниеУслугОбороты.Регистратор КАК Регистратор,
  ОказаниеУслугОбороты.Клиент КАК Клиент,
  ОказаниеУслугОбороты.Услуга КАК Услуга,
  ОказаниеУслугОбороты.Сотрудник КАК Сотрудник,
  ОказаниеУслугОбороты.СуммаОборот КАК СуммаОборот
ИЗ
  РегистрНакопления.ОказаниеУслуг.Обороты(, , Регистратор, ) КАК ОказаниеУслугОбороты
```

- Авто (Добавляются поля - периоды)


### Задание параметров
```bsl
ВЫБРАТЬ
  ОказаниеУслугОбороты.Период КАК Период,
  ОказаниеУслугОбороты.Регистратор КАК Регистратор,
  ОказаниеУслугОбороты.Клиент КАК Клиент,
  ОказаниеУслугОбороты.СуммаОборот КАК СуммаОборот
ИЗ
  РегистрНакопления.ОказаниеУслуг.Обороты(
      ,
      ,
      Регистратор,
      Сотрудник = &Сотрудние
      И Услуга = &Услуга) КАК ОказаниеУслугОбороты
```

```
ВЫБРАТЬ
  ТоварыНаСкладеОстатки.Склады КАК Склады,
  ТоварыНаСкладеОстатки.КоличествоОстаток КАК КоличествоОстаток
ИЗ
  РегистрНакопления.ТоварыНаСкладе.Остатки(КонецПериода(&ДатаВЗапрос, День), ) КАК ТоварыНаСкладеОстатки
```

Остатки могут принимать и отрицательные значения

```bsl
ВЫБРАТЬ
  ПродажаТовараТовары.Товар КАК Товар,
  ПродажаТовараТовары.Ссылка.Склад КАК Склад,
  СУММА(ПродажаТовараТовары.Количество) КАК Количество
ПОМЕСТИТЬ втТовары
ИЗ
  Документ.ПродажаТовара.Товары КАК ПродажаТовараТовары
ГДЕ
  ПродажаТовараТовары.Ссылка = &Документ

СГРУППИРОВАТЬ ПО
  ПродажаТовараТовары.Товар,
  ПродажаТовараТовары.Ссылка.Склад
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  втТовары.Товар КАК Товар,
  ЕСТЬNULL(ТоварыНаСкладеОстатки.КоличествоОстаток, 0) - втТовары.Количество КАК Остаток
ИЗ
  втТовары КАК втТовары
    ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладе.Остатки(&ДатаДокумента, (Склады, Товары) В (Выбрать вт.Склад, вт.Товар из втТовары как вт)) КАК ТоварыНаСкладеОстатки
    ПО (втТовары.Товар = ТоварыНаСкладеОстатки.Товары
        И втТовары.Склад = ТоварыНаСкладеОстатки.Склады)
```


### Запросы с виртуальной таблицей ОстаткиИОбороты
```bsl
ВЫБРАТЬ
  ТоварыНаСкладеОстаткиИОбороты.Период КАК Период,
  ТоварыНаСкладеОстаткиИОбороты.Склады КАК Склады,
  ТоварыНаСкладеОстаткиИОбороты.Товары КАК Товары,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоНачальныйОстаток КАК НачальныйОстаток,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоПриход КАК Приход,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоРасход КАК Расход,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоКонечныйОстаток КАК КонечныйОстаток
ИЗ
  РегистрНакопления.ТоварыНаСкладе.ОстаткиИОбороты(, , Год, , ) КАК ТоварыНаСкладеОстаткиИОбороты
```

```
ВЫБРАТЬ
  ТоварыНаСкладеОстаткиИОбороты.Период КАК Период,
  ТоварыНаСкладеОстаткиИОбороты.Склады КАК Склады,
  ТоварыНаСкладеОстаткиИОбороты.Товары КАК Товары,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоНачальныйОстаток КАК НачальныйОстаток,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоПриход КАК Приход,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоРасход КАК Расход,
  ТоварыНаСкладеОстаткиИОбороты.КоличествоКонечныйОстаток КАК КонечныйОстаток
ИЗ
  РегистрНакопления.ТоварыНаСкладе.ОстаткиИОбороты(НачалоПериода(&ДатаНачала, День), КонецПериода(&ДатаКонца, День), День, Движения, ) КАК ТоварыНаСкладеОстаткиИОбороты
```

### Использование вложенного запроса в параметре виртуальной таблицы ОстаткиТоваров.
```bsl
ВЫБРАТЬ
  ОстаткиТоваровОстатки.Номенклатура,
  ОстаткиТоваровОстатки.КоличествоОстаток
ИЗ
  РегистрНакопления.ОстаткиТоваров.Остатки(
      ,
      Номенклатура В
        (ВЫБРАТЬ
          Товары.Номенклатура
        ИЗ
          Документ.РеализацияТоваровУслуг.Товары КАК Товары
        ГДЕ
          Товары.Ссылка = &Ссылка)) КАК ОстаткиТоваровОстатки
```



## Тета соединения в запросах

### Вывод соседних элементов из последовательности
```bsl
//Первым делом создаем последовательность

ВЫБРАТЬ
  1 КАК Значение
ПОМЕСТИТЬ ВТ_СписокЗначений

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  2

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  7

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  8

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  9

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  15

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  16

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  27

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  28

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
  110
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  ВТ_СписокЗначений.Значение КАК ВерхняяГраница,
  МАКСИМУМ(ЕСТЬNULL(ВТ_СписокЗначений_ДляСвязи.Значение, 0)) КАК НижняяГраница
ПОМЕСТИТЬ ВТ_ПреднаборГраниц
ИЗ
  ВТ_СписокЗначений КАК ВТ_СписокЗначений
    ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СписокЗначений КАК ВТ_СписокЗначений_ДляСвязи
    ПО ВТ_СписокЗначений.Значение > ВТ_СписокЗначений_ДляСвязи.Значение

СГРУППИРОВАТЬ ПО
  ВТ_СписокЗначений.Значение

ИМЕЮЩИЕ
  ВТ_СписокЗначений.Значение - МАКСИМУМ(ЕСТЬNULL(ВТ_СписокЗначений_ДляСвязи.Значение, 0)) > 1
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
  ВТ_ПреднаборГраниц.НижняяГраница + 1 КАК НижняяГраница,
  ВТ_ПреднаборГраниц.ВерхняяГраница - 1 КАК ВерхняяГраница,
  ВТ_ПреднаборГраниц.ВерхняяГраница - ВТ_ПреднаборГраниц.НижняяГраница - 1 КАК ДлинаРазрыва
ИЗ
  ВТ_ПреднаборГраниц КАК ВТ_ПреднаборГраниц

УПОРЯДОЧИТЬ ПО
  НижняяГраница
```

### Пример тета соединения в запросе
```bsl
// Получим таблицу ВТ_ПТиУ
// "ДатаЗаписи" это дата документа на начало дня
ВЫБРАТЬ РАЗРЕШЕННЫЕ
  ПоступлениеТоваровУслуг.Ссылка КАК Ссылка,
  ПоступлениеТоваровУслуг.Дата,
  НАЧАЛОПЕРИОДА(ПоступлениеТоваровУслуг.Дата, ДЕНЬ) КАК ДатаЗаписи
ПОМЕСТИТЬ ВТ_ПТиУ
ИЗ
  Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
ГДЕ
  ПоступлениеТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
  И ПоступлениеТоваровУслуг.Проведен
  И НЕ ПоступлениеТоваровУслуг.ПометкаУдаления
;

////////////////////////////////////////////////////////////////////////////////
// Получим таблицу ВТ_ЗаписиРегистра 
// Для получения уникальных записей и получения максимальной даты
// группируем таблицу по полям "ДатаЗаписи" И "Валюта"
ВЫБРАТЬ
  ВТ_ПТиУ.ДатаЗаписи,
  МАКСИМУМ(КурсыВалют.Период) КАК Период,
  КурсыВалют.Валюта
ПОМЕСТИТЬ ВТ_ЗаписиРегистра
ИЗ
  ВТ_ПТиУ КАК ВТ_ПТиУ
    ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
    ПО ВТ_ПТиУ.ДатаЗаписи >= КурсыВалют.Период
      И (КурсыВалют.Валюта = &Валюта)

СГРУППИРОВАТЬ ПО
  ВТ_ПТиУ.ДатаЗаписи,
  КурсыВалют.Валюта
;

////////////////////////////////////////////////////////////////////////////////
// Получим итоговую таблицу
ВЫБРАТЬ
  ВТ_ЗаписиРегистра.ДатаЗаписи КАК ДатаЗаписи,
  КурсыВалют.Валюта,
  КурсыВалют.Курс
ИЗ
  ВТ_ЗаписиРегистра КАК ВТ_ЗаписиРегистра
    ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
    ПО ВТ_ЗаписиРегистра.Период = КурсыВалют.Период
      И ВТ_ЗаписиРегистра.Валюта = КурсыВалют.Валюта

УПОРЯДОЧИТЬ ПО
  ДатаЗаписи
```


Итоги отличаются от группировок тем, что в группировках заменяются поля, а в итогах дополняются.
